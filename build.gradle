plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'org.springframework.boot' version "$spring_boot_version"
    id 'com.google.cloud.tools.jib' version '1.8.0'
}

ext {
    github_org = 'DiscordBolt'
    project_name = 'BoltBot'
    artifact_group = 'com.discordbolt'
}

group "$artifact_group"
version "$project_version"

sourceCompatibility = JavaVersion.VERSION_12
targetCompatibility = JavaVersion.VERSION_12

bootJar {
    baseName = "$project_name"
    version = "$project_version"
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "$spring_boot_version"
    // Spring Data
    //implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb', version: "$spring_boot_version"
    // Spring MongoDB Reactive
    //implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb-reactive', version: "$spring_boot_version"
    // Discord4J
    implementation group: 'com.discord4j', name: 'discord4j-core', version: "$d4j_version"
    // Reflections (For BotModules)
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'
    // @NotNull annotations
    implementation group: 'org.checkerframework', name: 'checker', version: '2.1.12'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "$spring_boot_version"
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.18.3'
    testCompile group: 'de.flapdoodle.embed', name: 'de.flapdoodle.embed.mongo'
}

processResources {
    doLast {
        file("$buildDir/resources/main/application.properties").withWriter { w ->
            Properties p = new Properties()
            p.load(new FileInputStream(new File('src/main/resources/application.properties')))
            p['boltbot.version'] = "$project_version".toString()
            p['boltbot.commit'] = getCommitHash()
            p['discord.token'] = System.getenv('DISCORD_TOKEN')
            if (getBranchName() != 'master') {
                p['spring.data.mongodb.database'] = 'BoltBot-Dev'
                p['discord.token'] = System.getenv('DISCORD_TOKEN_DEV')
            }
            p.store w, null
        }
    }
}

static def getCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

/*
 * Does not work in GitHub Actions/Workflows.
 */
static def getBranchName() {
    'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
}

jib {
    from {
        image = 'openjdk:12'
    }
    to {
        image = "techtony96/boltbot:$project_version-".toString() + getCommitHash()
        switch (project.findProperty('GitBranch')) {
            case 'master':
                tags = ['latest']
                break
            case 'dev':
                tags = ['dev-latest']
                break
            default:
                project.logger.lifecycle('Unknown Branch: ' + project.findProperty('GitBranch'))
        }
        auth {
            username = 'techtony96'
            password = project.findProperty('DockerPassword')
        }
    }
    container {
        jvmFlags = ['-Xms512m', '-Xmx512m']
        mainClass = 'com.discordbolt.boltbot.BoltBot'
        args = ['--spring.config.location=/app/resources/application.properties']
    }
}
